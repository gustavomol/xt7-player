' Gambas class file

'Copyright (C) 2007, 2008 Antonio Orefice
' Gambas class file
Public AlreadyLoaded As Boolean = False 'true if the form loaded it's settings from disk at least one time
Public Sub MenuLoadDefault_Click()
  Dim MySettings As New SettingsClass
  MySettings.Load(Me, global.confpath & "/" & global.CurrentProfile)
End

Public Sub MenuSaveDefault_Click()
  Dim MySettings As New SettingsClass
  MySettings.Save(Me, global.confpath & "/" & global.CurrentProfile)
End

Public Sub MenuSaveToAll_Click()
  Dim profilename, profilenamesplitted As String
  Dim MySettings As New SettingsClass
  If MyQuestion.Ask("This will save this window settings\n to all profiles found but the 'Factory_defaults' one", "Proceed", "Don't") = 2 Then
    Return
      Else
     For Each profilename In Dir(global.confpath & "/", "*.profile")
        If Stat(global.confpath & "/" & profilename, True).type = gb.Directory Then
          profilenamesplitted = Split(profilename, ".")[0]
          If profilenamesplitted <> "Factory_Defaults" Then
            MySettings.Save(Me, global.confpath & "/" & profilenamesplitted)
          Endif
        Endif
     Next 'profilename
  Endif
End

Public Sub Form_Open()
  Global.Center(Fmain, Me)
End

Public Sub Sliders_Change()
  If (Last Is Slider) And (Last.next Is Spinbox) Then
    Try Last.Next.value = Last.value
    If (FMain.FullyLoaded Or ApplyEqCheckbox.value) Then FMain.mplayer.Send(Last.tag & " " & Last.value & " 1")
  Endif
End

Public Sub SpinBoxes_Change()
  If (Last Is Spinbox) And (Last.previous Is Slider) Then
    Try Last.previous.value = Last.value
  Endif
End

Public Sub Clears_Click()
  If (Last Is Toolbutton) And (Last.next Is Slider) And (Last.next.next Is Spinbox) Then
    Try Last.next.value = 0
    Try Last.next.next.value = 0
  Endif
End

Public Sub MenuItemHint_Click()
  MyQuestion.ask(("If equalizer doesn't work, you can try to:\n\n- Change video output driver\n- Enable eq2 and hue filters under General Options\n- Add Eq2 filter in chains window\n- Add Hue filter in chains window"), "Ok", "")
End

Public Sub ApplyEqSettings()
  FMain.mplayer.Send("osd 0") 'disable osd
  Wait 0.1
  FMain.mplayer.Send("brightness" & " " & spinbox1.value & " 1")
  FMain.mplayer.Send("contrast" & " " & spinbox2.value & " 1")
  FMain.mplayer.Send("gamma" & " " & spinbox3.value & " 1")
  FMain.mplayer.Send("hue" & " " & spinbox4.value & " 1")
  FMain.mplayer.Send("saturation" & " " & spinbox5.value & " 1")
  Wait 0.1
  FMain.mplayer.Send("osd 1") 'enable osd
End

Public Sub ApplyEqCheckbox_Click()
  If Last.value Then
    ApplyEqSettings()
  Endif
End

